C UTEP Electronic Structure Lab (2020)
C
C *****************************************************************
C
       SUBROUTINE ATCUBE(NARR,NPTS,R,WTS,NPHI,NTHET,RSPH,RBOX,
     &                   ERRMAX,ALMIN,ALMAX,AFUDGE,NPOW)
C ORIGINAL VERSION BY MARK R PEDERSON (1989)
C
C NARR:   SIZE OF R AND WTS
C NPTS:   NUMBER OF POINTS CREATED
C R,WTS:  COORDINATES AND WEIGHTS OF MESH POINTS
C NPHI:   NUMBER OF PHI POINTS IN WEDGE DEFINED BY RBOX > X > Y > Z
C NTHET:  NUMBER OF THETA POINTS IN WEDGE DEFINED BY RBOX > X > Y > Z
C RSPH:   RADIUS OF SPHERE
C RBOX:   SIZE OF BOX 
C ERRMAX: MAX. ERROR FOR RADIAL MESHES
C ALMIN:  MINIMUM GAUSSIAN DECAY CONSTANT
C ALMAX:  MAXIMUM GAUSSIAN DECAY CONSTANT
C AFUDGE: FACTOR BETWEEN SUCCESSIVE TEST ALPHAS
C NPOW:   MAXIMUM POWER OF TEST FUNCTION
C
C MXANG:  MAXIMUM SIZE OF THETA AND PHI MESH
C MXRAD:  MAXIMUM SIZE OF RADIAL MESH
C
        IMPLICIT REAL*8 (A-H,O-Z)
        PARAMETER (MXANG= 10)
        PARAMETER (MXANG2=2*MXANG)
        PARAMETER (MXRAD= 100)
        DIMENSION R(3,*),WTS(*)
        DIMENSION XPHI(MXANG),XTHET(MXANG2),XRAD(MXRAD)
        DIMENSION WPHI(MXANG),WTHET(MXANG2),WRAD(MXRAD)
        SAVE
C
C SETUP AND CHECK
C
        NTHET2=2*NTHET
        PD4=ATAN(1.0D0)
        PI= 4*PD4
        NANG=MAX(NPHI,NTHET)
        IF (NANG.GT.MXANG) THEN
         write(6,*)'ATCUBE: MXANG MUST BE AT LEAST: ',NANG
         CALL STOPIT
        END IF
        VOL=0.0D0
        IPTS=0
C
C TRANSFORMATION PHI=U**2 LEADS TO A MESH THAT INTEGRATES ALL ODD
C POWERS OF PHI IN (0,PI/4) UP TO 4*NPHI-1. THE TAYLOR EXPANSION 
C OF THE PHI INTEGRAND IN PHI=0 HAS ONLY ODD POWERS IN PHI.
C
        UP=PD4**2
        ZED=0.0D0
        CALL GAUSSP(ZED,UP,NPHI,XPHI,WPHI)
C
C TRANSFORM PHI POINTS AND WEIGHTS
C
        DO 10 I=1,NPHI
         XPHI(I)=SQRT(XPHI(I))
         WPHI(I)=WPHI(I)/(2*XPHI(I))
   10   CONTINUE
C
C THETA POINTS: MESH THAT INTEGRATES ONLY EVEN POWERS OF (PI/2-THETA)
C UP TO 4*NPHI-2. THE THETA INTEGRAND IS A FUNCTION EXPANDABLE IN A
C GERADE FUNCTION IN THETA=PI/2.
C
        DO 60 I=1,NPHI
         SINPHI=SIN(XPHI(I))
         COSPHI=COS(XPHI(I))
         TLM=ATAN(1.0D0/SINPHI)
         TUM=PI-TLM
         CALL GAUSSP(TLM,TUM,NTHET2,XTHET,WTHET)
C
C R POINTS: VARIATIONAL MESH
C
         DO 50 J=1,NTHET
          SINTHET=SIN(XTHET(J))
          COSTHET=COS(XTHET(J))
          RLM=RSPH
          RUM=RBOX/(SINTHET*COSPHI)
          CALL RADMSH(MXRAD,RLM,RUM,ERRMAX,ALMIN,ALMAX,AFUDGE,NPOW,
     &                NRAD,XRAD,WRAD)
          NPTS=IPTS+NRAD
          IF(NPTS.GT.NARR)THEN
           write(6,*)'ATCUBE: NPTS > NARR, NARR= ',NARR,' NPTS= ',NPTS
           CALL STOPIT
          END IF
          DO 40 K=1,NRAD
           IPTS=IPTS+1
           R(1,IPTS)=XRAD(K)*SINTHET*COSPHI
           R(2,IPTS)=XRAD(K)*SINTHET*SINPHI
           R(3,IPTS)=XRAD(K)*COSTHET
           WTS(IPTS)=WPHI(I)*WTHET(J)*WRAD(K)*SINTHET
           VOL=VOL+WTS(IPTS)
   40     CONTINUE
   50    CONTINUE
   60   CONTINUE
        VOL=VOL*48
C
C ORDER EM
C
        NPTS=IPTS
        DO 90 IPTS=1,NPTS
         DO 80 JPTS=IPTS+1,NPTS
          RI=R(1,IPTS)**2+R(2,IPTS)**2+R(3,IPTS)**2
          RJ=R(1,JPTS)**2+R(2,JPTS)**2+R(3,JPTS)**2
          IF(RJ.LT.RI)THEN
           DO 70 K=1,3
            PSAVE=R(K,JPTS)
            R(K,JPTS)=R(K,IPTS)
            R(K,IPTS)=PSAVE
   70      CONTINUE
           PSAVE=WTS(JPTS)
           WTS(JPTS)=WTS(IPTS)
           WTS(IPTS)=PSAVE
          END IF
   80    CONTINUE
   90   CONTINUE
        RETURN
       END
