C UTEP Electronic Structure Lab (2020)
      SUBROUTINE WRITEFRC(NCALC)
C
C     WRITE FORCES AND COORDINATES IN FRCOUT AND VIBINP FILES
C
C     BY ULISES REVELES, AUG. 2013.
C
C     ------------------------------------------------------------------
C
C     --- GLOBAL VARIABLES ---
C  
      use common2,only : ETOTAL,E_UP,E_DN,EFIELD,DIPOLE,FTOT,GMAX,
     %                   IFUIDT,NIDENT,RIDT,SPNNET,ZELC,ZNUC
      use mpidat1,only : MYGROUP
C
C     --- LOCAL VARIABLES ---
C
      INTEGER NCALC
C
      INTEGER MAXCHG
      PARAMETER (MAXCHG = 56)
C
      INTEGER I,IID,ID_GMAX,IZE,IZN
      REAL*8 GRAD
C
      INTEGER ALLOCATION
      REAL*8,ALLOCATABLE :: PMASS(:)
      CHARACTER :: GROUP_TXT*10
      CHARACTER :: COMMAND*30

C
C     ------------------------------------------------------------------
C
C     --- ALLOCATE LOCAL FIELDS ---
C
      ALLOCATE(PMASS(NIDENT),STAT=ALLOCATION)
      IF (ALLOCATION.GT.0) THEN
        WRITE(*,*) 'ERROR IN WRITEFRC: ALLOCATION FAILED'
      END IF
C
C     --- GET ATOMIC MASSES ---
C
      DO IID=1,NIDENT
        IZN= NINT(ZNUC(IFUIDT(IID)))
        IF ((IZN.GE.1).AND.(IZN.LE.MAXCHG)) THEN
          CALL GET_MASS(IZN,PMASS(IID))
        ELSE
          PMASS(IID)=1.0D10
        END IF
      END DO
C
C     --- OPEN FRCOUT AND VIBINP FILES ---
C
      OPEN(98,FILE='FRCOUT',FORM='FORMATTED',STATUS='UNKNOWN')
      OPEN(99,FILE='VIBINP',FORM='FORMATTED',STATUS='UNKNOWN')
      REWIND(98)
      REWIND(99)
C
C     --- UPDATE NET SPIN MOMENT ---
C
      SPNNET = E_UP - E_DN
C
C     --- NOW WRITE ---
C
      WRITE(98,1040) ETOTAL,NCALC
      WRITE(99,1080) NIDENT,E_UP,E_DN
      WRITE(99,1090)
      WRITE(99,1070)(PMASS(IID), IID=1,NIDENT)
      WRITE(99,1040) ETOTAL,NCALC
C
C     --- FIND THE MAXIMUM FORCE ---

      GMAX=0.0D0
      ID_GMAX=0
      DO IID=1,NIDENT
        GRAD=0.0D0
        DO I=1,3
          GRAD=GRAD+FTOT(I,IID)**2
        ENDDO
        GRAD=SQRT(GRAD)
        IF (GMAX.LT.GRAD) ID_GMAX=IID
        GMAX=MAX(GMAX,GRAD)
      ENDDO
C
C     --- NOW WRITE COORDINATES, FORCES, DIPOLE AND EFIELD ---
C
      DO IID=1,NIDENT
        IZE= NINT(ZELC(IFUIDT(IID)))
        IZN= NINT(ZNUC(IFUIDT(IID)))
        WRITE(98,1050)(RIDT(I,IID), I=1,3),IZE,IZN
        IF (IID.EQ.ID_GMAX) THEN
          WRITE(98,1061)(FTOT(I,IID), I=1,3)
        ELSE
          WRITE(98,1060)(FTOT(I,IID), I=1,3)
        ENDIF
C
        WRITE(99,1050)(RIDT(I,IID), I=1,3),IZE,IZN
        WRITE(99,1060)(FTOT(I,IID), I=1,3)
      END DO
C
      WRITE(98,1060)(DIPOLE(I), I=1,3)
      WRITE(98,1060)(EFIELD(I), I=1,3)
      WRITE(99,1060)(DIPOLE(I), I=1,3)
      WRITE(99,1060)(EFIELD(I), I=1,3)
C
C     --- DEALLOCATE LOCAL FIELD ---
C
      DEALLOCATE(PMASS,STAT=ALLOCATION)
      IF (ALLOCATION.GT.0) THEN
        WRITE(*,*) 'ERROR IN WRITEFRC: DEALLOCATION FAILED'
      END IF
C
C     --- CLOSE FILES ---
C
      CLOSE(98)
      CLOSE(99)
#ifdef GROUP
      WRITE(GROUP_TXT,'(1I0)') MYGROUP
      GROUP_TXT=ADJUSTL(GROUP_TXT)
      COMMAND='mv FRCOUT FRCOUT.G'//TRIM(GROUP_TXT)
      CALL SYSTEM(COMMAND)
#endif
C
C     --- FORMAT STATEMENTS ---
C    
 1040 FORMAT(1X,F30.10,1X,I5,' Energy and Symbol NCALC')
 1050 FORMAT(3(1X,F20.8),2(1X,I3))
 1060 FORMAT(3(1X,D20.12))
 1061 FORMAT(3(1X,D20.12),'  <--- MAX-FORCE')
 1070 FORMAT(3(1X,F20.6))
 1080 FORMAT(1X,I5,' 0.02 0.005 ',2(1X,F12.6),'  NAT,DX,DE,EUP,EDN')
 1090 FORMAT(' 300.0 0.0  1 6.0 ',26X,'  TEMP,WAVL,IBROAD,FWHM')
C
C     ------------------------------------------------------------------
C
      END 
